extends CharacterBody2D
## Player control logic.
##
## Tutorial(Top Down Survival Shooter In Godot | Part 2 - Player Movement): https://youtu.be/oSX6x7LT4e0
## Accessed on: 15.09.2026
##
## Explanation of 8 directional movement by kidscancode: https://kidscancode.org/godot_recipes/4.x/2d/8_direction/
## Accessed on: 21.09.2026
## 
## Tutorial(Top Down Survival Shooter In Godot | Part 5 - Shooting): https://youtu.be/xalHXyDtfpc
## Accessed on: 23.09.2026
## 
## Tutorial(Top Down Survival Shooter In Godot | Part 8 - Game Over): https://youtu.be/_-e7t5Fby_0
## Accessed on: 23.09.2026


signal shoot

@onready var sfxwalk = $sfxwalk

const START_SPEED : int = 200
const BOOST_SPEED : int = 400
const NORMAL_SHOT : float = 0.5
const FAST_SHOT : float = 0.1
var speed : int
var can_shoot : bool
var screen_size : Vector2

func _ready():
	screen_size = get_viewport_rect().size
	reset_control()

func reset_control():
	can_shoot = true
	position = screen_size / 2
	speed = START_SPEED
	$ShotTimer.wait_time = NORMAL_SHOT
	

func get_input():
	# keyboard input
	var input_dir = Input.get_vector("left", "right", "up", "down")
	velocity = input_dir.normalized() * speed
	
	# walk sfx
	if velocity.length() > 0:
		if not sfxwalk.playing:
			sfxwalk.play()
	else:
		sfxwalk.stop()
	
	# mouse clicks
	if Input.is_mouse_button_pressed(MOUSE_BUTTON_LEFT) and can_shoot:
		var dir = get_global_mouse_position() - position
		shoot.emit(position, dir)
		can_shoot = false
		$ShotTimer.start()
	
	# mouse clicks
	if Input.is_mouse_button_pressed(MOUSE_BUTTON_LEFT) and can_shoot:
		var dir = get_global_mouse_position() - position
		shoot.emit(position, dir)
		can_shoot = false
		$ShotTimer.start()

func _physics_process(_delta):
	# player movement
	get_input()
	move_and_slide()
	
	# limit movement to window size
	position = position.clamp(Vector2.ZERO, screen_size)
	
	# player rotation
	var mouse = get_local_mouse_position()
	var angle = snappedf(mouse.angle(), PI/ 4) / (PI / 4)
	angle = wrapi(int(angle), 0, 8)
	
	$AnimatedSprite2D.animation = "walk" +str(angle)
	
	#player animation
	if velocity.length() != 0:
		$AnimatedSprite2D.play()
	else:
		$AnimatedSprite2D.stop()
		$AnimatedSprite2D.frame = 1
		
func boost():
	$boostTimer.start()
	speed = BOOST_SPEED
	
func quick_fire():
	$fastfireTimer.start()
	$ShotTimer.wait_time = FAST_SHOT



func _on_shot_timer_timeout():
	can_shoot = true
	
func _on_boost_timer_timeout() -> void:
	speed = START_SPEED


func _on_fastfire_timer_timeout() -> void:
	$ShotTimer.wait_time = NORMAL_SHOT
